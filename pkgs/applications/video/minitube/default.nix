{ stdenv, fetchFromGitHub, makeWrapper, qt5
# "Free" API key generated by nckx <tobias.geerinckx.rice@gmail.com>
, withAPIKey ? "AIzaSyBtFgbln3bu1swQC-naMxMtKh384D3xJZE" }:

stdenv.mkDerivation rec {
  name = "minitube-${version}";
  version = "2.5.2";

  src = fetchFromGitHub {
    sha256 = "0flygyiyx4d7la9pmg78ndzs13z0xnkp3hsakf3h5dcplah84kc5";
    rev = version;
    repo = "minitube";
    owner = "flaviotordini";
  };

  NIX_CFLAGS_COMPILE = "-I${qt5.phonon}/include/phonon4qt5";

  buildInputs = [ qt5.phonon qt5.phonon-backend-vlc qt5.qtbase qt5.qtscript ];
  nativeBuildInputs = [ makeWrapper qt5.qmakeHook ];

  qmakeFlags = [ "DEFINES+=APP_GOOGLE_API_KEY=${withAPIKey}" ];

  enableParallelBuilding = true;

  postInstall = ''
    wrapProgram $out/bin/minitube \
      --prefix QT_PLUGIN_PATH : "${qt5.phonon-backend-vlc}/lib/qt5/plugins"
  '';

  meta = with stdenv.lib; {
    description = "Stand-alone YouTube video player";
    longDescription = ''
      Watch YouTube videos in a new way: you type a keyword, Minitube gives
      you an endless video stream. Minitube is not about cloning the YouTube
      website, it aims to create a new TV-like experience.
    '';
    homepage = http://flavio.tordini.org/minitube;
    license = licenses.gpl3Plus;
    platforms = platforms.linux;
    maintainers = with maintainers; [ nckx ];
  };
}
